apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion corda_platform_version.toInteger()
    minimumPlatformVersion corda_platform_version.toInteger()
    workflow {
        name "Shield MVP"
        vendor "Corda Open Source"
        licence "Apache License, Version 2.0"
        versionId 1
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven {
        url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-solutions-releases'
    }

}

dependencies {
    // Corda dependencies.
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    // For testing.
    testCompile "junit:junit:$junit_version"
    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"
    testCompile "com.r3.businessnetworks:membership-service:2.0"
    testCompile "com.r3.businessnetworks:membership-service-contracts-and-states:2.0"


    // CorDapp dependencies.
    cordapp project(":contracts")

    // Accounts
    cordaCompile "$confidential_id_release_group:ci-workflows:$confidential_id_release_version"
    cordaCompile "$accounts_release_group:accounts-workflows:$accounts_release_version"

    // Token SDK dependencies.
    cordaCompile "$tokens_release_group:tokens-money:$tokens_release_version"
    cordaCompile "$tokens_release_group:tokens-workflows:$tokens_release_version"

    // Business Network Membership Service (BNMS)
    cordapp "com.r3.businessnetworks:membership-service:2.0"
    cordapp "com.r3.businessnetworks:membership-service-contracts-and-states:2.0"

    // Stellar Java SDK
    compile 'com.github.stellar:java-stellar-sdk:0.12.0'

    // Java Web Token to validate expiration of the Signet Token
    compile 'io.jsonwebtoken:jjwt-api:0.10.7'
    runtime 'io.jsonwebtoken:jjwt-impl:0.10.7', 'io.jsonwebtoken:jjwt-orgjson:0.10.7'
}

task integrationTest(type: Test, dependsOn: [jar]) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
